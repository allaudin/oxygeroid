plugins {
    id "com.jfrog.bintray" version "1.7.3"
} // plugins

apply plugin: 'java'
apply plugin: 'maven'

version = rootProject.ext.version

//noinspection GroovyUnusedAssignment
sourceCompatibility = "1.7"
//noinspection GroovyUnusedAssignment
targetCompatibility = "1.7"


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
} // sourceJar

artifacts {
    archives sourcesJar
} // artifacts

assemble.dependsOn 'sourcesJar'

jar {
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    archiveName  "oxygeroid-annotations-${project.version}.jar"
} // fatJar

tasks.bintrayUpload.dependsOn 'assemble'

bintray {

    user = "${rootProject.user}"
    key = "${rootProject.key}"

    publish = rootProject.ext.publish
    override = rootProject.ext.override

    pkg {

        filesSpec {
            into "."
            from 'build/release'
        } // fileSpecs

        repo = "${rootProject.repo}"
        name = "${project.name}"
        licenses = rootProject.ext.licenses

        //noinspection GroovyAssignabilityCheck
        version {
            name = "${project.version}"
            released  = new Date()
            vcsTag = "${project.version}"
        } // version

    } // pkg

} // bintray

def localReleasePath = "${buildDir}/release"

uploadArchives {
    repositories.mavenDeployer {
        pom.groupId = rootProject.ext.groupId
        pom.artifactId = project.name
        pom.version = project.version
        repository(url: "file://${localReleasePath}")
    }
}

