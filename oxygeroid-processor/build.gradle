plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'java'
apply plugin: 'maven'

version = rootProject.ext.version

//noinspection GroovyUnusedAssignment
sourceCompatibility = "1.7"
//noinspection GroovyUnusedAssignment
targetCompatibility = "1.7"


ext {
    javapoet = '1.8.0'
    jdom = '2.0.2'
    localReleasePath = "${buildDir}/release"
}

jar {
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    archiveName "oxygeroid-processor-${project.version}.jar"
}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

assemble.dependsOn 'sourcesJar'

dependencies {
    compile "com.squareup:javapoet:${project.ext.javapoet}"
    compile "org.jdom:jdom:${project.ext.jdom}"
    compile project(":oxygeroid-annotations")
}


tasks.bintrayUpload.dependsOn 'uploadArchives'
tasks.uploadArchives.dependsOn 'assemble'


bintray {

    user = "${rootProject.user}"
    key = "${rootProject.key}"

    publish = rootProject.ext.publish
    override = rootProject.ext.override

    pkg {

        filesSpec {
            into "."
            from 'build/release'
        } // fileSpecs

        repo = "${rootProject.repo}"
        name = "${project.name}"
        licenses = rootProject.ext.licenses

        //noinspection GroovyAssignabilityCheck
        version {
            name = "${project.version}"
            released  = new Date()
            vcsTag = "${project.version}"
        } // version

    } // pkg

} // bintray


uploadArchives {
    repositories.mavenDeployer {
        pom.groupId = rootProject.ext.groupId
        pom.artifactId = project.name
        pom.version = project.version
        repository(url: "file://${project.ext.localReleasePath}")
    }
}

